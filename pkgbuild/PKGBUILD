# Maintainer: Bruno Pagani <archange@archlinux.org>
# Maintainer: BarnabediKartola
# Contributor: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Alexandre Filgueira <alexfilgueira@cinnarch.com>
# Contributor: Ner0

pkgname=nemo
pkgver=6.4.5
pkgrel=2
pkgdesc="Cinnamon file manager (Nautilus fork) with biglinux custons"
arch=(x86_64)
url="https://github.com/biglinux/${pkgname}"
license=(GPL)
depends=(cinnamon-desktop dconf gvfs exempi libexif libnotify libxml2
         python xapp vte3 jemalloc)
optdepends=('cinnamon-translations: i18n'
            'ffmpegthumbnailer: support for video thumbnails'
            'catdoc: search helpers support for legacy MS Office files'
            'ghostscript: search helpers support for PostScript files'
            'libgsf: search helpers support for MS Office files'
            'libreoffice: search helpers support for legacy MS Office powerpoint files'
            'odt2txt: search helpers support for LibreOffice files'
            'poppler: search helpers support for PDF')
makedepends=(meson samurai gobject-introspection intltool libgsf glib2-devel)
# source=(${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
source=("git+${url}.git")
sha512sums=('SKIP')
b2sums=('SKIP')

prepare() {
  # cd ${pkgname}-${pkgver}
  cd ${pkgname}
  # Rename 'Files' app name to avoid having the same as nautilus
  sed -i '/^\[Desktop Entry/,/^\[Desktop Action/ s/^Name\(.*\)=.*/Name\1=Nemo/' data/nemo.desktop.in
}

build() {
#   arch-meson --libexecdir=lib/${pkgname} ${pkgname}-${pkgver} build
  arch-meson --libexecdir=lib/${pkgname} ${pkgname} build
  samu -C build
}

package() {
  DESTDIR="${pkgdir}" samu -C build install
}
